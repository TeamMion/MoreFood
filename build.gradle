buildscript
{
    repositories
    {
        jcenter()
        
        maven
        {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        
        maven
        {
            url = "https://plugins.gradle.org/m2"
        }
    }
    
    dependencies
    {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT"
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.0.8"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "com.matthewprenger.cursegradle"

version = modver + (release != "release" ? "-" + release + "." + releasever : "")
group= modgroup
archivesBaseName = modid + "_" + mcversion

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

minecraft
{
    version = mcversion + "-" + forgeversion
    
    runDir = "run"
    
    mappings = mcpmappings
    
    makeObfSourceJar = false
}

dependencies
{
    compile files("libs/mioncore-" + mioncore + "-deobf.jar")
}

task deobfJar(type : Jar) {
    from sourceSets.main.output
    
    classifier = "deobf"
}

task deobfSourceJar(type : Jar) {
    from sourceSets.main.java
    from sourceSets.main.resources
    
    classifier = "sources"
}

artifacts
{
    archives deobfJar
    archives deobfSourceJar
}

curseforge
{
    apiKey = System.getenv().CURSE_API_KEY ? System.getenv().CURSE_API_KEY : "devBuild"
    
    project
    {
        id = curse_id
        
        changelog = ""
        
        releaseType = release == "rc" ? "release" : release
        
        addGameVersion mcversion
        
        mainArtifact jar
        
        addArtifact deobfSourceJar
        addArtifact deobfJar
        
        relations
        {
            requiredLibrary "mioncore"
        }
    }
}

processResources
{
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    
    from(sourceSets.main.resources.srcDirs)
    {
        include "mcmod.info"
        
        expand "version" : project.version, "mcversion" : project.minecraft.version
    }
    
    from(sourceSets.main.resources.srcDirs)
    {
        exclude "mcmod.info"
    }
}
