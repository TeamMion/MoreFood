buildscript {
    repositories {
        jcenter()
        
        maven {
            url = "http://files.minecraftforge.net/maven"
        }
        
        maven {
            url = "https://plugins.gradle.org/m2"
        }
    }
    
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT"
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.0.8"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "com.matthewprenger.cursegradle"

group = "org.teammion.morefood"
archivesBaseName = "${modid}_${mc}"

sourceCompatibility = targetCompatibility = "1.8"

compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
    version = "${mc}-${forge}"
    
    runDir = "run"
    
    mappings = mcp
    
    makeObfSourceJar = false
}

jar {
    from sourceSets.api.output
    from sourceSets.main.output
}

task deobfJar(type : Jar) {
    from sourceSets.api.output
    from sourceSets.main.output
    
    classifier = "deobf"
}

task deobfSourceJar(type : Jar) {
    from sourceSets.api.java
    from sourceSets.main.java
    from sourceSets.main.resources
    
    classifier = "sources"
}

artifacts {
    archives deobfJar
    archives deobfSourceJar
}

curseforge {
    apiKey = System.getenv().CURSE_API_KEY ? System.getenv().CURSE_API_KEY : "devBuild"
    
    project {
        id = curse
        
        changelog = "Forge version: ${mc}-${forge}"
        
        releaseType = release
        
        addGameVersion mc
        
        mainArtifact jar
        
        addArtifact deobfSourceJar
        addArtifact deobfJar
    }
}

processResources {
    inputs.property "modid", modid
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        
        expand "modid" : modid, "version" : project.version, "mcversion" : project.minecraft.version
    }
    
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}
